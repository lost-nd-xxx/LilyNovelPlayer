
＃シナリオの名前・ゲーム内変数の名前・ゲーム内変数の内容には、
＃なるべく区切り字を含まないでください。
＃里々にある区切り字
＃、､，,
＃独自の区切り字
＃|:

＃システムの動作でsplit関数を多用しています。
＃そのため、シナリオの動作でsplitを使うことはなるべく避けた方が良いです。

＃＊以下の改行が自動で\nに変換されない設定を適用しています。
＃改行したい時は手動で\nを書いてください。



＠master.残したくないオートセーブのリスト
|qが押された|φ
|でばっぐるーむ|φ
|コマンド入力脱出|φ
|シナリオＥ|φ
|シナリオＥ１|φ
|シナリオＥ２|φ
|シナリオＥ３|φ
|シナリオＦ|φ
|シナリオＦ１|φ
|シナリオＦ２|φ
|シナリオＦ３|φ
|シナリオＧ|φ
|シナリオＧ１|φ
|シナリオＨ|φ
|シナリオＨ１|φ
|チュートリアルＡ７|φ
|テストシナリオから確認無しでタイトルに戻る|φ
|テスト１|φ
|テスト２|φ
|テスト３|φ
|残存セーブ削除しない１|φ
|残存セーブ削除しない２|φ
|残存セーブ削除しない３|φ
|残存セーブ削除完了|φ
|残存セーブ削除実行１|φ
|残存セーブ削除実行２|φ
|残存セーブ削除実行３|φ
|残存セーブ削除１|φ
|残存セーブ削除２|
＃オートセーブを残したくないチェックポイントの名前（|で挟んだ単一行）



＠master.残したくないオートセーブがあれば初期化
（split（b1）（s_（ゲームID）.オートセーブ）（b1）|（b1）0（b1）1）φ
（when,φ
（count（b1）（master.残したくないオートセーブのリスト）（b1）|（S1）|）==1,φ
（set,s_（ゲームID）.オートセーブ,|）φ
）
＃オートセーブを残したくないところのデータがあったらオートセーブを初期化する



＊master.起動
＃最初に来るのは必ず「＊【シェルフォルダ名】.起動」

＄__tmp	（master.残したくないオートセーブがあれば初期化）
＃オートセーブを残したくないところのデータがあったらオートセーブを初期化する

＄__tmp	（call,ラベル通過,起動,オートセーブ不可）
＃オートセーブはさせたくないが中断からの復帰はさせたい

＄文字色.current	（文字色.normal）
＃文字色を普通の色に設定する

＄文字色強制	
＃設定を解除しておく

\![raise,master.タイトル]
＃文字色を反映させるため、いったん区切り



＊master.タイトル
＄サーフェス_ウィンドウ	2
＃タイトルバーの文字を\s[2]へ変える

＄__tmp	（変数読み出しg,,タイトルバー）
＄サーフェス_ウィンドウ	（when,（変数「__tmp」の存在）==1&&（compare,（__tmp）,特殊）==1,3,2）
＃グローバル変数にタイトルバーを特殊にするフラグがあればウィンドウを\s[3]にする

＄__tmp	
＃不要な変数を消しておく

＄master.クリック待ちマーカー設定	
＃独自のクリック待ち設定を無効にする
＃このように普通の変数を読み書きすることもできるが、変数名は他のゲームとの被りを気にする必要がある
＃そのため、ゲーム固有の情報を読み書きしたい場合、フォルダ名を先頭に付けることを推奨

＄バックログ状態	0
＃バックログを有効にするかどうか。1で有効

（バックログ全削除）
（スキップ解除）
（オート解除）
＃スキップとオートを解除

（メニュー不可）
＃メニュー系の操作を全部不可にする

＄メニュー状態.コンフィグ	1
＄メニュー状態.ギャラリー	1
＄メニュー状態.ロード	1
＃許可していいものだけ許可する

（freeimage）
＃それまでの描画状態をクリアする
＃引数無しはレイヤ４枚すべてをクリアする

（背景３）
＃タイトル画面の背景　dic_snippet.txtも参照のこと

（音停止）
＃すべての音を止める

\![close,inputbox,__SYSTEM_ALL_INPUT__]
＃インプットボックスが出ていたら消しておく

：
\f[shadowcolor,#333]\f[shadowstyle,outline]
＃文字に黒の縁取りをつける

\_q\n[40]
\f[height,48]\f[align,center]masterの\nさんぷるげーむ

\f[height,default]

\*
\_l[0,0]\n[500]
＃位置合わせ

（nop,（split,（s_（ゲームID）.オートセーブ）,|,0,0））
＃オートセーブから再開できるかどうかの下調べ
（when、（文「（ゲームID）.（S0）」の存在）==1、
\_l[50,]\__q[OnResumeGame,オートセーブ]つづきから\__q\n[110]
＃オートセーブから再開できるならさせる
）

\_l[50,]\__q[master.チュートリアルはじめから]はじめから\__q\n[110]
＃「master.チュートリアルはじめから」へ飛ばす
＃この飛ばし方をすると中断時の再開地点にならない
＃飛び先はdic_scenario.txtを参照のこと

\_l[50,]\__q[メニュー.ロード]ロード\__q\n[110]
＃セーブデータを選んでロード

\_l[50,]\__q[メニュー.コンフィグ]コンフィグ\__q\n[110]
＃コンフィグ画面へ飛ばす

\_l[50,]\__q[OnMenu,ギャラリー]ギャラリー\__q\n[110]
＃ギャラリー画面へ飛ばす

\_l[50,]\__q[メニュー.終了ダイアログ]おわる\__q
＃終了するかどうか聞く

\_l[0,0]\n[500]
＃位置合わせ

\_l[300,]★マニュアル等\n[110]

\_l[300,]\__q[シナリオ制作の手引を開く]シナリオ制作の手引\__q\n[110]
＃タグ一覧を開く

\_l[300,]\__q[独自タグ一覧を開く]独自タグ一覧\__q\n[110]
＃タグ一覧を開く

\_l[300,]\__q[master.テスト３]補助識別子生成\__q\n[200]
＃「master.テスト３」へ飛ばす
＃飛び先はdic_scenario2.txtを参照のこと

\_l[300,]★動作の見本\n[110]

\_l[300,]\__q[master.テスト１]ゲーム内変数の読み書き\__q\n[110]
＃「master.テスト１」へ飛ばす
＃飛び先はdic_scenario2.txtを参照のこと

\_l[300,]\__q[master.テスト２]ユーザ入力\__q\n[110]
＃「master.テスト２」へ飛ばす
＃飛び先はdic_scenario2.txtを参照のこと

\_l[300,]\__q[master.シナリオＨ]ギャラリーの状態を操作\__q
＃「master.テスト４」へ飛ばす
＃飛び先はdic_scenario2.txtを参照のこと

\_q



＊独自タグ一覧を開く
\![open,browser,file:///help/tags.html]
＞master.起動

＊シナリオ制作の手引を開く
\![open,browser,file:///help/hint.html]
＞master.起動
