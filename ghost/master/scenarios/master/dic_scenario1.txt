
＊master.チュートリアルはじめから
（call,ラベル通過,チュートリアルはじめから,オートセーブ不可）
＃中断からの復帰はOKだがオートセーブ地点にはしたくない場合

＄s_master.オートセーブ	|
＃はじめからなのでオートセーブのみ初期化
＃他のセーブスロットやグローバルセーブには影響しない

＄master.クリック待ちマーカー設定	（__default.クリック待ちマーカー）
＃独自のクリック待ち設定を無効にする

（スキップ解除）
（オート解除）
＃スキップとオートを解除する

＄バックログ状態	1
＃バックログを有効に

（メニュー不可）
＃メニュー操作を許可しない

（freeimage）
＃画面描画をクリアする。
＃引数無しはレイヤ４枚すべてをクリアする

：\_q
\_l[10,10]この先、音が出ます。\n
\_l[10,]音量に注意してください。
\_q\x
（nextjump,チュートリアルＡ０）



＊master.チュートリアルＡ０
（メニュー許可）
＃メニュー操作を許可する

（背景明）
（女子Ａ１）
（男子Ａ１）
（字枠１）
＃画像を描画する
＃dic_snippet.txtも参照のこと

：（名札,【女子学生】,20,220）\n
＃名前表示的なもの
（ボイス１）
\_l[20,]こんにちは！\w9\n
（ボイス１）
\_l[20,]このゴーストは\w3\f[bold,1]Lily Novel Player\f[bold,0]\w3といいます。\w9\n
（ボイス１）
\_l[20,]私たち共々、\w5どうぞよろしくね。
＃\_l[x座標,]で位置指定をしないと左端から文字が出る
（nextset,チュートリアルＡ,１）
＃次のシーンへのクリック待ちをしつつ、
＃次のシナリオ接頭辞と数値の開始値を設定する

＊master.チュートリアルＡ１
（メニュー許可）
（背景明）
（女子Ａ１）
（男子Ａ１）
（字枠１）
＃メニュー許可状態・画像・音楽は、nextや選択肢での移動後、毎回指定する必要がある
＃変化がなければ再描画はされない
＃セーブデータからのロード時のための措置

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]このゴースト…\w5…\w5略して\f[bold,1]LNP\f[bold,0]を使えば、\w5\n
（ボイス１）
\_l[20,]SSPで動作する\f[bold,1]ノベルゲームを作れる\f[bold,0]んだよ。\w9\n
（ボイス１）
\_l[20,]SHIORIは里々を使っているの。
（next）
＃次のシーンへのクリック待ち

＊master.チュートリアルＡ２
（メニュー許可）
（背景明）
（freeimage,2）
（女子Ａ１）
（男子Ａ５）
（字枠１）

：（名札,【男子学生】,20,220）\n
（ボイス２）
\_l[20,]しかし、\w5ゲームっていうなら、\w5\n
（ボイス２）
\_l[20,]背景と音楽が欲しいよな。
（next）

＊master.チュートリアルＡ３
（メニュー許可）
（背景明）
（freeimage,2）
（女子Ａ７）
（男子Ａ１）
（字枠１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]もちろんあるよ。\w9ちょっと待ってね。
（next）

＊master.チュートリアルＡ４
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ７）
（男子Ｂ３）
（字枠１）
（音楽１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]こんな感じだよ。\w9\n
（ボイス１）
\_l[20,]タグ辞書はタイトル画面から開けるから、\w5\n
（ボイス１）
\_l[20,]後で見てみてね。
（next）

＊master.チュートリアルＡ５
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ１）
（男子Ｂ７）
（字枠１）
（音楽１）

：（名札,【男子学生】,20,220）\n
（ボイス２）
\_l[20,]おっ、\w5いいな。\w9\n
（ボイス２）
\_l[20,]画像の切り替え効果みたいなものは無いのか？
（next）

＊master.チュートリアルＡ６
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ１）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]無いよ。\w9\n
（ボイス１）
\_l[20,]でも、\w5工夫次第で多少はなんとかなるかも。
（next）

＊master.チュートリアルＡ７
（メニュー不可）
（freeimage）
（移動背景１）\w9\w9
（移動背景２）\w9\w9
（移動背景３）\w9\w9
（移動背景４）\w9\w9
（nextjump,チュートリアルＡ８）

＊master.チュートリアルＡ８
（メニュー許可）
（背景２）
（字枠１）
（女子Ｃ１）
（音楽２）

：（名札,【女子学生】,180,240）\n
（ボイス１）
\_l[180,]…\w5…\w5とまあ、\w5こんな感じに。
（next）

＊master.チュートリアルＡ９
（メニュー許可）
（背景２）
（字枠１）
（男子Ｃ１）
（音楽２）

：（名札,【男子学生】,180,240）\n
（ボイス２）
\_l[180,]メッセージウィンドウに\n
（ボイス２）
\_l[180,]顔が入るタイプか。\w9\n
（ボイス２）
\_l[180,]これもいいな。
（next）

＊master.チュートリアルＡ１０
（メニュー許可）
（背景２）
（freeimage,1,2）
（女子Ｄ）
（男子Ｄ）
（字枠２）
（音楽２）

：
\_l[20,30]\f[color,#a5e12d]「ノベルゲームといえば、\w5この形式も要るよね」\w9\n[150]
\_l[20,]（文字色.current）　女子学生はドヤ顔を決めた。\w9\n[150]
\_l[20,]\f[color,#fb961f]「まあわかるけど…\w5…\w5その顔、\w5ウザいぞ」\w9\n[150]
\_l[20,]\f[color,#a5e12d]\f[height,150%]「見えてないでしょ」\w9\f[height,default]\n[150]
\_l[20,]\f[color,#fb961f]「見えなくても、\w5わかるときはある…\w5…」\w9\n[150]
\_l[20,]\f[color,#a5e12d]「…\w5…\w5ちなみに、\w5バックログ機能もあるよ」
（文字色.current）
（next）
＃フォントサイズ変え　色変えなども同じ要領で
＃色を戻す時は（文字色.current）を入れる
＃\f[default]を入れると設定中バルーンのデフォルトに戻ってしまう

＊master.チュートリアルＡ１１
（メニュー許可）
（freeimage）
（背景１）
（女子Ｂ１）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]あとは選択肢だね。\w9好きな位置に出せるよ。\n[120]
\_l[20,]\![*]（選択肢,メッセージウインドウに出したり,チュートリアルＢ０,チュートリアル分岐,下）\n
（選択肢、\_l[0,150]\f[align,center]\_b["assets/preset/choice_base1.png",inline,--option=use_self_alpha,--option=foreground]\_l[0,163]\f[align,center]画像を敷いてボタンっぽくしたり、チュートリアルＢ０、チュートリアル分岐、上）


＃\_l[0,150]\f[align,center]
＃\_b["assets/preset/choice_base1.png",inline,--option=use_self_alpha,--option=foreground]
＃\_l[0,155]\f[align,center]


＊master.チュートリアルＢ０
（メニュー許可）
（背景１）
（女子Ｂ１）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【男子学生】,20,220）\n
（ボイス２）
\_l[20,]なるほどな。\w9\n
（ボイス２）
\_l[20,]分岐したりもできるのか？
（nextset,チュートリアルＢ,１）

＊master.チュートリアルＢ１
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ７）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]もちろん。\w9\n
（ボイス１）
\_l[20,]さっき選択肢を押したとき、\w5\n
（ボイス１）
\_l[20,]どっちを押したか覚えてる？
（next）

＊master.チュートリアルＢ２
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ１）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【男子学生】,20,220）\n
（ボイス２）
\_l[20,]（変数読み出し、チュートリアル分岐）だろ。\w9わかってるって。
（next）

＊master.チュートリアルＢ３
（メニュー許可）
（背景１）
（女子Ｂ１）
（男子Ｂ１）
（字枠１）
（音楽１）

：（名札,【女子学生】,20,220）\n
（ボイス１）
\_l[20,]じゃあ最後に、\w5タイトルバーをいじってみるね。\w9\n
（ボイス１）
\_l[20,]せっかくだから、\w5分岐してみよっか。\n[150]
\_l[20,]\![*]（選択肢,タイトルバーを特殊にする,チュートリアルＣ１,__global__タイトルバー,特殊）\n
\_l[20,]\![*]（選択肢,タイトルバーを通常にする,チュートリアルＣ２,__global__タイトルバー,通常）

＊master.チュートリアルＣ１
＄サーフェス_ウィンドウ	3
＃ウィンドウを\s[3](ピンク色)にする
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ７）
（男子Ｂ７）
（字枠１）
（音楽１）

：（名札,【二人】,20,220）\n
（ボイス同時）
\_l[20,]ありがとうございました！\w9\n[150]
\f[align,center]〜おしまい〜
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]

＊master.チュートリアルＣ２
＄サーフェス_ウィンドウ	2
＃ウィンドウを\s[2](通常色)にする
（メニュー許可）
（背景１）
（freeimage,2）
（女子Ｂ７）
（男子Ｂ７）
（字枠１）
（音楽１）

：（名札,【二人】,20,220）\n
（ボイス同時）
\_l[20,]ありがとうございました！\w9\n[150]
\f[align,center]〜おしまい〜
（スキップ解除）
（オート解除）
\x
\![raise,メニュー.確認無しでタイトルに戻る]


